plugins {
    id 'application'
    id 'eclipse'
    id 'com.google.cloud.tools.jib' version '2.8.0'
    id 'org.javamodularity.moduleplugin' version '1.7.0'
    id 'life.expert.archidoc' version '1.0.11'
}

wrapper.gradleVersion = '6.8.3'
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
modularity.improveEclipseClasspathFile()

repositories {
    mavenCentral()
    google()
}

dependencies {
    configurations.implementation.canBeResolved = true

    implementation 'ws.schild:jave-all-deps:3.0.1'
    implementation 'org.slf4j:slf4j-nop:2.0.0-alpha1'

    testImplementation 'com.ginsberg:junit5-system-exit:1.1.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

application {
    mainModule = moduleName
    mainClassName = 'cyou.obliquerays.media.RadioRecProcess'
    applicationDefaultJvmArgs = ['-Duser.language=ja -Duser.country=JP -Duser.timezone=Asia/Tokyo']
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withSourcesJar()
    withJavadocJar()
}

jar {
    version = buildMajor + '.' + buildMinor + '.' + buildRevision
    manifest {
        attributes('Name': 'cyou.obliquerays.media')
        attributes('Specification-Title': project.name)
        attributes('Specification-Version': version)
        attributes('Specification-Vendor': 'cyou.obliquerays')
        attributes('Implementation-Title': project.name)
        attributes('Implementation-Version': version)
        attributes('Implementation-Vendor': 'cyou.obliquerays')
        attributes('Automatic-Module-Name': moduleName)
    }
    doLast {
        file('gradle/release').mkdirs()
        delete {
            delete files('gradle/release')
        }
        copy {
            from configurations.implementation
            into 'gradle/release'
        }
        copy{
            from jar
            into 'gradle/release'
        }
        def currentRevision = buildRevision.toInteger() + 1
        def File propsFile = file('gradle.properties')
        if (propsFile.canWrite()) {
            def Properties props = new Properties()
            props.load(new FileReader(propsFile))
            props['buildRevision'] = currentRevision.toString()
            props.store(propsFile.newWriter(), null)
        }
    }
}

tasks.withType(Javadoc) {
    failOnError = false
    options {
        memberLevel = JavadocMemberLevel.PRIVATE
        encoding = 'UTF-8'
        links("https://docs.oracle.com/javase/jp/11/docs/api/")
    }
}

compileTestJava {
//    onlyIf { project.hasProperty('goTest') }
}

test {
    onlyIf { project.hasProperty('goTest') }
    testLogging.showStandardStreams = true
    moduleOptions {
        runOnClasspath = true
    }
    useJUnitPlatform()
}

jib {
    from {
        image = 'ffmpeg-alpine'
    }
    to {
        image = 'radio-recorder' // YOUR_IMAGE_NAME
    }
}

archidoc {
    file "${project.projectDir}/doc/dot/aclassdiagram.dot"
    packages = ['cyou.obliquerays']
    enableAllInfo()
    enableInterClassDependencies()
    enableExternalClasses()
}
